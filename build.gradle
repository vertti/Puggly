apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.ben-manes.versions'

sourceCompatibility = 1.8
group = 'com.nitorcreations'
version = '0.6.0'

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.10.1'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
//        html.enabled = true
    }
}

dependencies {
    compile 'org.springframework:spring-web:4.1.6.RELEASE'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.google.code.gson:gson:2.3.1'
    testCompile 'org.eclipse.jetty:jetty-server:9.3.0.RC1'
    testCompile 'org.eclipse.jetty:jetty-servlet:9.3.0.RC1'
    testCompile 'org.apache.httpcomponents:httpclient:4.5'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'nl.jqno.equalsverifier:equalsverifier:1.7.2'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'Puggly'
                    packaging 'jar'
                    description 'Pluggable Java-configured ServletFilter'
                    url 'https://github.com/NitorCreations/Puggly'

                    scm {
                        url 'scm:git@github.com:NitorCreations/Puggly.git'
                        connection 'scm:git@github.com:NitorCreations/Puggly.git'
                        developerConnection 'scm:git@github.com:NitorCreations/Puggly.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'vertti'
                            name 'Janne Sinivirta'
                        }
                    }
                }
            }
        }
    }
}
